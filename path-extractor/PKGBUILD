pkgname=path-extractor
pkgver=0.2.0
pkgrel=1
pkgdesc='A unix filter which outputs the filepaths found in stdin'
arch=('x86_64')
url="https://github.com/edi9999/$pkgname"
license=('GPL')
makedepends=('go')
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('7d6c7463e833305e6d27c63727fec1029651bfe8bca5e8d23ac7db920c2066e7')

gopath=gopath/src/github.com/edi9999/$pkgname

prepare() {
  mkdir -p $(dirname $gopath)
  ln -rTsf $pkgname-$pkgver $gopath
  export GOPATH="$srcdir"/gopath
  cd $gopath
  go get
}

build() {
  export GOPATH="$srcdir"/gopath
  cd $gopath
  go install \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "-extldflags $LDFLAGS" \
    -v ./...
}

package() {
  install -Dm755 gopath/bin/$pkgname "$pkgdir"/usr/bin/$pkgname
}

